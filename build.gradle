buildscript {
  repositories {
    mavenCentral()
    gradlePluginPortal()
    mavenLocal()
    google()
    maven { url 'https://central.sonatype.com/repository/maven-snapshots/' }
  }
  dependencies {
    classpath "com.android.tools.build:gradle:8.9.3"

  }
}

task generateVersionFile {
    doLast {
        def assetsDir = new File("${project.rootDir}/assets") 
        
        if (!assetsDir.exists()) {
            println "Advertencia: No se encontró la carpeta assets para escribir la versión."
            return
        }
        
        def gitHash = 'git rev-parse --short HEAD'.execute().text.trim()
        def gitTag = null
        
        def tagsOutput = 'git tag --points-at HEAD'.execute().text.trim()
        gitTag = tagsOutput.split('\n').find { it.startsWith('v') }
        
        String finalVersion
        if(gitTag){
            finalVersion = "${gitTag} (Build: ${gitHash})"
        }else{
            finalVersion = "${projectVersion} (Build: ${gitHash})"
        }

        def versionFile = new File(assetsDir, "version.properties")
        versionFile.text = "version=${finalVersion}"

        println "Versión generada: ${finalVersion}"
    }
}

allprojects {
  tasks.matching { it.name.startsWith('processResources') }.configureEach {
    dependsOn rootProject.tasks.generateVersionFile
  }
  apply plugin: 'eclipse'
  apply plugin: 'idea'

  // This allows you to "Build and run using IntelliJ IDEA", an option in IDEA's Settings.
  idea {
    module {
      outputDir file('build/classes/java/main')
      testOutputDir file('build/classes/java/test')
    }
  }
}

configure(subprojects - project(':android')) {
  apply plugin: 'java-library'
  sourceCompatibility = 8

  // From https://lyze.dev/2021/04/29/libGDX-Internal-Assets-List/
  // The article can be helpful when using assets.txt in your project.
  tasks.register('generateAssetList') {
    inputs.dir("${project.rootDir}/assets/")
    // projectFolder/assets
    File assetsFolder = new File("${project.rootDir}/assets/")
    // projectFolder/assets/assets.txt
    File assetsFile = new File(assetsFolder, "assets.txt")
    // delete that file in case we've already created it
    assetsFile.delete()

    // iterate through all files inside that folder
    // convert it to a relative path
    // and append it to the file assets.txt
    fileTree(assetsFolder).collect { assetsFolder.relativePath(it) }.sort().each {
      assetsFile.append(it + "\n")
    }
  }
  processResources.dependsOn 'generateAssetList'

  compileJava {
    options.incremental = true
  }
}

subprojects {
  version = "$projectVersion"
  ext.appName = 'IAsRevenge'
  repositories {
    mavenCentral()
//    maven { url 'https://s01.oss.sonatype.org' }
    // You may want to remove the following line if you have errors downloading dependencies.
    mavenLocal()
//    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
//    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://central.sonatype.com/repository/maven-snapshots/' }
    maven { url 'https://jitpack.io' }
  }
}

eclipse.project.name = 'IAsRevenge' + '-parent'
